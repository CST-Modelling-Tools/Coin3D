macro(strfy _input _postfix)
  get_filename_component(_basename "${_input}" NAME_WE)
  get_filename_component(_suffix "${_input}" EXT)
  get_filename_component(_path "${_input}" PATH)
  string(SUBSTRING ${_suffix} 1 -1 _suffix)
  string(TOUPPER "SO_${_basename}_${_suffix}_H" COIN_HEADER_DEF)
  string(TOUPPER ${_basename} COIN_TEXTVAR_NAME)
  set(COIN_TEXTVAR_NAME "${COIN_TEXTVAR_NAME}${_postfix}")
  #message(STATUS "Parse: ${CMAKE_SOURCE_DIR}/${_input}")
  FILE(READ ${CMAKE_SOURCE_DIR}/${_input} f0)
  STRING(REGEX REPLACE "\\\\" "\\\\\\\\" f1 "${f0}")
  STRING(REGEX REPLACE "\"" "\\\\\"" f2 "${f1}")
  STRING(REGEX REPLACE "\r?\n" "\\\\n\"\n  \"" COIN_STR_SOURCE_CODE "${f2}")
  configure_file(strfytemplate.cmake.in "${_path}/${_basename}.h")
endmacro()

file(GLOB_RECURSE COIN_SHADER_GLSL RELATIVE ${CMAKE_SOURCE_DIR} *.glsl)
foreach(_input_file ${COIN_SHADER_GLSL})
  strfy(${_input_file} "_shadersource")
endforeach()
file(GLOB_RECURSE COIN_SHADER_INVENTOR RELATIVE ${CMAKE_SOURCE_DIR} *.iv)
foreach(_input_file ${COIN_SHADER_INVENTOR})
  strfy(${_input_file} "_draggergeometry")
endforeach()

install(DIRECTORY scxml/navigation/ DESTINATION ${CMAKE_INSTALL_DATADIR}/${PROJECT_NAME}/scxml/navigation COMPONENT development FILES_MATCHING PATTERN [cep]*.xml)
install(DIRECTORY draggerDefaults/  DESTINATION ${CMAKE_INSTALL_DATADIR}/${PROJECT_NAME}/draggerDefaults  COMPONENT development FILES_MATCHING PATTERN *.iv)
install(DIRECTORY shaders/lights/   DESTINATION ${CMAKE_INSTALL_DATADIR}/${PROJECT_NAME}/shaders/lights   COMPONENT development FILES_MATCHING PATTERN *.glsl)
install(DIRECTORY shaders/vsm/      DESTINATION ${CMAKE_INSTALL_DATADIR}/${PROJECT_NAME}/shaders/vsm      COMPONENT development FILES_MATCHING PATTERN *.glsl)
